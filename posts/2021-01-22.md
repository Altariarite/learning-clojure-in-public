## 4 Clojure Problem 43

I got stuck on atom and decided to take a spin to do another day of 'living clojure training'. Problem 43 was quite interesting and thought I should record my thought process.

Problem description:

Write a function which reverses the interleave process into x number of subsequences.

```clojure
(= (__ [1 2 3 4 5 6] 2) '((1 3 5) (2 4 6))) 
(= (__ (range 9) 3) '((0 3 6) (1 4 7) (2 5 8))) 
(= (__ (range 10) 5) '((0 5) (1 6) (2 7) (3 8) (4 9))
```

I immediately thought of `partition` and tried the following in repl:

```clojure
clj꞉w3.day3꞉> 
(partition 2 [1 2 3 4])
((1 2) (3 4))
clj꞉w3.day3꞉> 
(map first (partition 2 [1 2 3 4]))
(1 3)
clj꞉w3.day3꞉> 
(nth [1 2 3 4] 0)
1
```

So I just need to find a way to repeatedly apply:

```clojure
(map #(nth % 0) '((1 2) (3 4)))
(map #(nth % 1) '((1 2) (3 4)))
```

and `conj` the results together. I then wrote the first version below with a sorta imperative mindset.

```clojure
(fn
  [x n]
  (let [p (partition n x)
        len (count (first p))]
    (loop [idx 0
           res []]
      (if (= idx len) res
          (recur (inc idx)
                 (conj res (map #(nth % idx) p)))))))
```

Then I see two brilliant versions with different mindsets:

```clojure
; notice (map list '(1 2) '(3 4) '(5 6)) => ((1 3 5) (2 4 6))
;; and (apply map f [x y z]) <=> (map f x y z)
;; It might help to think of 'map' and 'vector' as "slipping inside" the argument
;; list, and then become executable form
#(apply map list (partition %2 %))

;; also can use take-nth
#(for [x (range %2)] (take-nth %2 (drop x %)))
```

The `map` in clojure is very versatile, should keep that in mind...

I then realized my code is basically a list comprehension, so I shortened it into:

```clojure
(fn
  [x n]
  (let [p (partition n x)]
    (for [idx (range n)] (map #(nth % idx) p))))
```

